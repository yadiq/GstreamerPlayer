apply plugin: 'com.android.application'

android {
    compileSdk 32
    //ndkVersion '21.4.7075529' //gst<24
    ndkVersion '25.1.8937393' //gst>=24

    defaultConfig {
        applicationId "com.hqumath.gstreamer"
        minSdk 21//so加固时限制21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 32
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "arm64-v8a"//主流手机
            //abiFilters "armeabi-v7a"//电视盒子
            //abiFilters "x86"
            //abiFilters "x86_64"
        }
        //更多编译参数
        externalNativeBuild {
            ndkBuild {
                //gstreamer库位置
                def gstRoot
                if (project.hasProperty('gstAndroidRoot'))
                    gstRoot = project.gstAndroidRoot
                else
                    gstRoot = System.env.GSTREAMER_ROOT_ANDROID
                if (gstRoot == null)
                    throw new GradleException('GSTREAMER_ROOT_ANDROID must be set, or "gstAndroidRoot" must be defined in your gradle.properties in the top level directory of the unpacked universal GStreamer Android binaries')
                //参数
                arguments "NDK_APPLICATION_MK=src/main/jni/Application.mk", "GSTREAMER_JAVA_SRC_DIR=src/main/java", "GSTREAMER_ROOT_ANDROID=$gstRoot", "GSTREAMER_ASSETS_DIR=src/main/assets"
                //so名称
                targets "gstreamer_player"
                // All archs except MIPS and MIPS64 are supported
                //abiFilters  'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    buildFeatures {
        viewBinding = true
    }

    //开启动态库编译
    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/Android.mk')
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
}
